apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

repositories {
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
}

dependencies {
    compile project(':worldedit-core')
    compile 'com.sk89q:dummypermscompat:1.8'
    compile 'org.bukkit:bukkit:1.13-R0.1-SNAPSHOT' // zzz
    compile 'org.bstats:bstats-bukkit:1.4'
    compile "io.papermc:paperlib:1.0.1"
    compileOnly "net.milkbowl.vault:VaultAPI:1.7"
    testCompile 'org.mockito:mockito-core:1.9.0-rc1'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand 'internalVersion': project.internalVersion
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

jar {
    manifest {
        attributes("Class-Path": "truezip.jar WorldEdit/truezip.jar js.jar WorldEdit/js.jar",
                   "WorldEdit-Version": version)
    }
}

shadowJar {
    dependencies {
        include(dependency(':worldedit-core'))
        relocate ("org.bstats", "com.sk89q.worldedit.bukkit.bstats") {
            include(dependency("org.bstats:bstats-bukkit:1.4"))
        }
        relocate ("io.papermc.lib", "com.sk89q.worldedit.bukkit.paperlib") {
            include(dependency("io.papermc:paperlib:1.0.1"))
        }
    }
}

build.dependsOn(shadowJar)
