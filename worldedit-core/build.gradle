plugins {
    id("net.ltgt.apt") version "0.21"
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'net.ltgt.apt-eclipse'
apply plugin: 'net.ltgt.apt-idea'

configurations.all { Configuration it ->
    it.resolutionStrategy { ResolutionStrategy rs ->
        rs.force("com.google.guava:guava:21.0")
    }
}

dependencies {
    compile project(':worldedit-libs:core')
    compile 'de.schlichtherle:truezip:6.8.3'
    compile 'rhino:js:1.7R2'
    compile 'org.yaml:snakeyaml:1.9'
    compile 'com.google.guava:guava:21.0'
    compile 'com.google.code.findbugs:jsr305:1.3.9'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.slf4j:slf4j-api:1.7.26'

    compileOnly project(':worldedit-libs:core:ap')
    annotationProcessor project(':worldedit-libs:core:ap')
    annotationProcessor "com.google.guava:guava:21.0"
    def avVersion = "1.6.5"
    compileOnly "com.google.auto.value:auto-value-annotations:$avVersion"
    annotationProcessor "com.google.auto.value:auto-value:$avVersion"
    //compile 'net.sf.trove4j:trove4j:3.0.3'
    testCompile 'org.mockito:mockito-core:1.9.0-rc1'
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add("-Aarg.name.key.prefix=")
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/legacy/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}
